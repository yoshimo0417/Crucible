/////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////
//            Pyroclastic Dragon
/////////////////////////////////////////////////////////////////////
//        Version 1
// Pyroclastic, 30th level Dragon
//
//
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// Initialize our Juggernaut
/////////////////////////////////////////////////////////////////////
IF
  Global("Prep","LOCALS",0)
  Detect([PC])
  Allegiance(Myself,ENEMY)
  DifficultyGT(HARD)
THEN
  RESPONSE #100
    EquipMostDamagingMelee() // use those claws!
    SetGlobal("MO_FireStorm","LOCALS",10)
    SetGlobal("Prep","LOCALS",1)
END

IF
  Global("Prep","LOCALS",0)
  Detect([PC])
  Allegiance(Myself,ENEMY)
  DifficultyLT(HARDEST)
THEN
  RESPONSE #100
    DestroyItem("MOBHA57C")
    SetGlobal("MO_FireStorm","LOCALS",0)
    EquipMostDamagingMelee() // use those claws!
    SetGlobal("Prep","LOCALS",1)
END

/////////////////////////////////////////////////////////////////////
// The Meat Of The Fight 
// Dragon Wisdom:
//  (1) "Best way to block punch: no be there." (thanks Mr. Miyagi)
//  (2) Nuke 'em 'till they glow.
//  (3) Use those special Dragon tricks on enemies in range. 
//  (4) Target Player1 (or the party), just at Player1 will target
//  Mellisan and ignore her summons. The fight ends if you kill 
//  Gorion's Ward.
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// Red Dragon Breath
// Spell Range 320, Speed 3, Once Every 18 seconds
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
  !GlobalTimerNotExpired("MO_REDDRAGONBREATH","LOCALS")
  Exists(StrongestOf)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_REDDRAGONBREATH","LOCALS",18)
    // "ReallyForce" allows us to take advantage of the full range of
    // this spell (which exceeds conventional line of sight). FK is
    // at least 40' tall and should have no trouble seeing to the end
    // of his cavern. 
    ForceSpell(StrongestOf,RED_DRAGON_BREATH)
    ReallyForceSpell(StrongestOf,RED_DRAGON_HIT)
END

/////////////////////////////////////////////////////////////////////
// Wing Buffet
// Spell Range 30, Speed 1, Unlimited 
// This is at the top of the list because we don't ever want to
// get hit (if at all possible). Keep pushing them back.
// Adjusting this from every 2 seconds to every 3 seconds. 
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
  Range(NearestEnemyOf(Myself),25)
  !GlobalTimerNotExpired("Buffet","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("Buffet","LOCALS",12)
    ForceSpell(NearestEnemyOf(Myself),DRAGON_WING_BUFFET)
END

/////////////////////////////////////////////////////////////////////
// Spell: Fire Storm
/////////////////////////////////////////////////////////////////////
IF
  OR(2)
    !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
    CheckStatGT(Myself,0,AURACLEANSING)
  !GlobalTimerNotExpired("MO_FireStormTimer","LOCALS")
  GlobalGT("MO_FireStorm","LOCALS",0)
  DifficultyGT(HARD)
  See([GOODCUTOFF])
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #50
    SetGlobal("MO_CastFireStorm","LOCALS",1)
    Continue()
  RESPONSE #50
    SetGlobal("MO_CastFireStorm","LOCALS",0)
    Continue()
END 

IF 
  OR(2)
    !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
    CheckStatGT(Myself,0,AURACLEANSING)
  !GlobalTimerNotExpired("MO_FireStormTimer","LOCALS")
  GlobalGT("MO_FireStorm","LOCALS",0)
  Global("MO_CastFireStorm","LOCALS",1)
  DifficultyGT(HARD)
  See(NearestEnemyOf(Myself))
  NumCreatureGT([GOODCUTOFF],3)
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !CheckStatGT(LastSeenBy(Myself),0,SANCTUARY)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStatGT(LastSeenBy(Myself),50,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),70,RESISTFIRE)
  !CheckSpellState(LastSeenBy(Myself),SI_EVOCATION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetGlobalTimer("MO_FireStormTimer","LOCALS",18)
    IncrementGlobal("MO_FireStorm","LOCALS",-1)
    SetInterrupt(FALSE)
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_FIRE_STORM)
    SetInterrupt(TRUE)
END

IF 
  OR(2)
    !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
    CheckStatGT(Myself,0,AURACLEANSING)
  !GlobalTimerNotExpired("MO_FireStormTimer","LOCALS")
  GlobalGT("MO_FireStorm","LOCALS",0)
  Global("MO_CastFireStorm","LOCALS",1)
  DifficultyGT(HARD)
  See(SecondNearestEnemyOf(Myself))
  NumCreatureGT([GOODCUTOFF],3)
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !CheckStatGT(LastSeenBy(Myself),0,SANCTUARY)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStatGT(LastSeenBy(Myself),50,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),70,RESISTFIRE)
  !CheckSpellState(LastSeenBy(Myself),SI_EVOCATION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetGlobalTimer("MO_FireStormTimer","LOCALS",18)
    IncrementGlobal("MO_FireStorm","LOCALS",-1)
    SetInterrupt(FALSE)
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_FIRE_STORM)
    SetInterrupt(TRUE)
END

IF 
  OR(2)
    !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
    CheckStatGT(Myself,0,AURACLEANSING)
  !GlobalTimerNotExpired("MO_FireStormTimer","LOCALS")
  GlobalGT("MO_FireStorm","LOCALS",0)
  Global("MO_CastFireStorm","LOCALS",1)
  DifficultyGT(HARD)
  See(ThirdNearestEnemyOf(Myself))
  NumCreatureGT([GOODCUTOFF],3)
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !CheckStatGT(LastSeenBy(Myself),0,SANCTUARY)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStatGT(LastSeenBy(Myself),50,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),70,RESISTFIRE)
  !CheckSpellState(LastSeenBy(Myself),SI_EVOCATION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetGlobalTimer("MO_FireStormTimer","LOCALS",18)
    IncrementGlobal("MO_FireStorm","LOCALS",-1)
    SetInterrupt(FALSE)
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_FIRE_STORM)
    SetInterrupt(TRUE)
END

IF 
  OR(2)
    !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
    CheckStatGT(Myself,0,AURACLEANSING)
  !GlobalTimerNotExpired("MO_FireStormTimer","LOCALS")
  GlobalGT("MO_FireStorm","LOCALS",0)
  Global("MO_CastFireStorm","LOCALS",1)
  DifficultyGT(HARD)
  See(FourthNearestEnemyOf(Myself))
  NumCreatureGT([GOODCUTOFF],3)
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !CheckStatGT(LastSeenBy(Myself),0,SANCTUARY)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStatGT(LastSeenBy(Myself),50,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),70,RESISTFIRE)
  !CheckSpellState(LastSeenBy(Myself),SI_EVOCATION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetGlobalTimer("MO_FireStormTimer","LOCALS",18)
    IncrementGlobal("MO_FireStorm","LOCALS",-1)
    SetInterrupt(FALSE)
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_FIRE_STORM)
    SetInterrupt(TRUE)
END

IF 
  OR(2)
    !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
    CheckStatGT(Myself,0,AURACLEANSING)
  !GlobalTimerNotExpired("MO_FireStormTimer","LOCALS")
  GlobalGT("MO_FireStorm","LOCALS",0)
  Global("MO_CastFireStorm","LOCALS",1)
  DifficultyGT(HARD)
  See(FifthNearestEnemyOf(Myself))
  NumCreatureGT([GOODCUTOFF],3)
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !CheckStatGT(LastSeenBy(Myself),0,SANCTUARY)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStatGT(LastSeenBy(Myself),50,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),70,RESISTFIRE)
  !CheckSpellState(LastSeenBy(Myself),SI_EVOCATION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetGlobalTimer("MO_FireStormTimer","LOCALS",18)
    IncrementGlobal("MO_FireStorm","LOCALS",-1)
    SetInterrupt(FALSE)
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_FIRE_STORM)
    SetInterrupt(TRUE)
END

IF 
  OR(2)
    !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
    CheckStatGT(Myself,0,AURACLEANSING)
  !GlobalTimerNotExpired("MO_FireStormTimer","LOCALS")
  GlobalGT("MO_FireStorm","LOCALS",0)
  Global("MO_CastFireStorm","LOCALS",1)
  DifficultyGT(HARD)
  See(SixthNearestEnemyOf(Myself))
  NumCreatureGT([GOODCUTOFF],3)
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !CheckStatGT(LastSeenBy(Myself),0,SANCTUARY)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(LastSeenBy(Myself),1,WIZARD_SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStatGT(LastSeenBy(Myself),50,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),70,RESISTFIRE)
  !CheckSpellState(LastSeenBy(Myself),SI_EVOCATION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetGlobalTimer("MO_FireStormTimer","LOCALS",18)
    IncrementGlobal("MO_FireStorm","LOCALS",-1)
    SetInterrupt(FALSE)
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_FIRE_STORM)
    SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////////
// Attack
/////////////////////////////////////////////////////////////////////
IF
  Global("MO_IsMoving","LOCALS",1)
  See(NearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",0)
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Global("MO_IsMoving","LOCALS",1)
  See(SecondNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",0)
    AttackOneRound(SecondNearestEnemyOf(Myself))
END

IF
  Global("MO_IsMoving","LOCALS",1)
  See(ThirdNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",0)
    AttackOneRound(ThirdNearestEnemyOf(Myself))
END

IF
  Global("MO_IsMoving","LOCALS",1)
  See(FourthNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",0)
    AttackOneRound(FourthNearestEnemyOf(Myself))
END

IF
  Global("MO_IsMoving","LOCALS",1)
  See(FifthNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",0)
    AttackOneRound(FifthNearestEnemyOf(Myself))
END

IF
  Global("MO_IsMoving","LOCALS",1)
  See(SixthNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",0)
    AttackOneRound(SixthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  DifficultyGT(HARD)
  See(NearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  Range(LastSeenBy(Myself),5)
  WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #25
    AttackOneRound(NearestEnemyOf(Myself))
  RESPONSE #75
    Continue()
END

IF
  ActionListEmpty()
  DifficultyGT(HARD)
  See(SecondNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  Range(LastSeenBy(Myself),5)
  WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #25
    AttackOneRound(SecondNearestEnemyOf(Myself))
  RESPONSE #75
    Continue()
END

IF
  ActionListEmpty()
  DifficultyGT(HARD)
  See(ThirdNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  Range(LastSeenBy(Myself),5)
  WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #25
    AttackOneRound(ThirdNearestEnemyOf(Myself))
  RESPONSE #75
    Continue()
END

IF
  ActionListEmpty()
  DifficultyGT(HARD)
  See(FourthNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  Range(LastSeenBy(Myself),5)
  WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #25
    AttackOneRound(FourthNearestEnemyOf(Myself))
  RESPONSE #75
    Continue()
END

IF
  ActionListEmpty()
  DifficultyGT(HARD)
  See(FifthNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  Range(LastSeenBy(Myself),5)
  WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #25
    AttackOneRound(FifthNearestEnemyOf(Myself))
  RESPONSE #75
    Continue()
END

IF
  ActionListEmpty()
  DifficultyGT(HARD)
  See(SixthNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  Range(LastSeenBy(Myself),5)
  WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #25
    AttackOneRound(SixthNearestEnemyOf(Myself))
  RESPONSE #75
    Continue()
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SecondNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SecondNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(ThirdNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(ThirdNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FourthNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FourthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FifthNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FifthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SixthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See("MOBHA44A")
  Allegiance(Myself,ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs("MOBHA44A",MAINHAND)
  WeaponCanDamage("MOBHA44A",MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound("MOBHA44A")
END

/////////////////////////////////////////////////////////////////////
// Find them! (Experimental)
/////////////////////////////////////////////////////////////////////
IF
  !Detect([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  OR(2)
    Global("Prep","LOCALS",1)
    TookDamage()
  Global("MO_IsMoving","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",1)
    MoveToObject(Player1)
END

IF
  ActionListEmpty()
  See(Player1)
  !See(Player2)
  Allegiance(Myself,ENEMY)
  !WeaponEffectiveVs(Player1,MAINHAND)
  !WeaponCanDamage(Player1,MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",1)
    MoveToObject(Player2)
END

IF
  ActionListEmpty()
  See(Player2)
  !See(Player3)
  Allegiance(Myself,ENEMY)
  !WeaponEffectiveVs(Player2,MAINHAND)
  !WeaponCanDamage(Player2,MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",1)
    MoveToObject(Player3)
END

IF
  ActionListEmpty()
  See(Player3)
  !See(Player4)
  Allegiance(Myself,ENEMY)
  !WeaponEffectiveVs(Player3,MAINHAND)
  !WeaponCanDamage(Player3,MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",1)
    MoveToObject(Player4)
END

IF
  ActionListEmpty()
  See(Player4)
  !See(Player5)
  Allegiance(Myself,ENEMY)
  !WeaponEffectiveVs(Player4,MAINHAND)
  !WeaponCanDamage(Player4,MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",1)
    MoveToObject(Player5)
END

IF
  ActionListEmpty()
  See(Player5)
  !See(Player6)
  Allegiance(Myself,ENEMY)
  !WeaponEffectiveVs(Player5,MAINHAND)
  !WeaponCanDamage(Player5,MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",1)
    MoveToObject(Player6)
END

IF
  ActionListEmpty()
  See(Player6)
  !See(Player1)
  Allegiance(Myself,ENEMY)
  !WeaponEffectiveVs(Player6,MAINHAND)
  !WeaponCanDamage(Player6,MAINHAND)
THEN
  RESPONSE #100
    SetGlobal("MO_IsMoving","LOCALS",1)
    MoveToObject(Player1)
END

